== Query Language

WARNING: Note that this is just a specification work in progress that is not yet implemented.

The query language that can be used with {projectname} is a super-set of JPQL and we like to call it JPQL.next.
It adds support for various more modern DBMS features that depending on the native DBMS support pass through as expected or are emulated.
This is a reference overview of the language, the specific features and the support matrices are in other sections that are referenced.

// See https://www.postgresql.org/docs/current/static/sql-select.html#SQL-FROM for inspiration
// See https://github.com/antlr/grammars-v4/blob/master/jpa/JPA.g4 for actual JPQL ANTLR grammar

=== Statement types

The normally known statement types are extended to support CTEs and set operations as well as the LIMIT and OFFSET clause.

[source,antlr4]
----
statement
    : with_clause? simple_statement ;
simple_statement
    : select_statement
    | from_statement
    | insert_statement
    | update_statement
    | delete_statement
    ;
with_clause : WITH RECURSIVE? cteEntityName '(' attributeName (',' attributeName)* ')' AS '(' simple_statement ')' ;
----

When defining a CTE, the _cteEntityName_ must be a valid JPA entity name or FQN for a JPA entity class.
All _columns_ of the defined attributes of the JPA entity class that is used for a CTE have to be declared as attributes within the parenthesis.
The statement in a CTE must provide the same amount of select expressions or returning attributes that bind by position to the declared CTE attributes.
If a CTE intends to reference itself for recursive processing, the RECURSIVE keyword must be added.

=== Select statement

A from statement is essentially like a select statement, but without the select part in the top level queries.

[source,antlr4]
----
from_statement      : from_specification window_clause? order_by_clause? ;
select_statement    : query_expression window_clause? order_by_clause? ;
subquery            : subquery_expression order_by_clause_subquery? ;

from_query_expression
    : ( from_specification | '(' from_query_expression ')' ) from_query_set_operator_expression* ;
query_expression
    : ( query_specification | '(' query_expression ')' ) set_operator_expression* ;
subquery_expression
    : ( subquery_specification | '(' subquery_expression ')' ) subquery_set_operator_expression* ;

set_operator
    : ( UNION | EXCEPT | INTERSECT ) ALL?;
from_query_set_operator_expression
    : set_operator ( from_specification | ( '(' from_query_expression ')' ) ) ;
set_operator_expression
    : set_operator ( query_specification | ( '(' query_expression ')' ) ) ;
subquery_set_operator_expression
    : set_operator ( subquery_specification | ( '(' subquery_expression ')' ) ) ;

query_specification
    : SELECT DISTINCT? select_list_elem ( ',' select_list_elem )*
      from_specification
    ;
----

Select statements can use set operators which will work on the scalar results as expected.

[source,antlr4]
----
from_specification :
      FROM from_elements
      keyset_clause?
      where_clause?
      group_by_clause?
      having_clause?
    ;
subquery_specification
    : SELECT DISTINCT? select_list_elem ( ',' select_list_elem )*
      FROM from_elements_subquery
      keyset_clause?
      where_clause?
      group_by_clause?
      having_clause?
    ;

keyset_clause
    : ( BEFORE | AFTER ) ( parameter_expression | '(' parameter_or_literal_expression ( ',' parameter_or_literal_expression )* ')' )
    ;

select_list_elem        : expression ( AS selectAlias )? ;
from_elements           : from_element ( ',' from_element )*
from_elements_subquery  : from_element_subquery ( ',' from_element_subquery )*

from_element            : from_group | ( '(' from_group ')' )
from_element_subquery   : from_group_subquery | ( '(' from_group_subquery ')' )

from_group              : from_item fetch_join*
from_group_subquery     : from_item_subquery join*

from_item
    : values_from_item
    | (
        entityName
      | ( OLD | NEW ) '(' entityName ')'
//      | '(' select_statement ')'
    ) ( AS? entityAlias )?
    ;
from_item_subquery
    : values_from_item
    | (
        entityName
      | ( OLD | NEW ) '(' entityName ')'
      | identification_variable '.' association_path_expression
//      | '(' select_statement ')'
    ) ( AS? entityAlias )?
    ;

// The syntax with a value count is the usual way to go
// But the inline value definition is for ad-hoc queries
values_from_item
    : (
        simpleTypeName '(' valueCount VALUES ')'
      | entityName ( '.' idName )? '(' valueCount VALUES ')'
      | simpleTypeName '(' VALUES values_item ( ',' values_item  )* ')'
      | entityName ( '.' idName )? '(' VALUES values_item ( ',' values_item  )* ')'
    ) AS? entityAlias
    ;

values_item : '(' literal_expression ( ',' literal_expression )* ')' ;
join_spec   : DEFAULT? ( INNER? | ( LEFT | RIGHT ) OUTER? ) JOIN ;

join        : join_spec from_join_element ON condition_expression ;
fetch_join  : join_spec FETCH? from_join_element ON condition_expression ;

from_join_element
    : (
        from_element
      | identification_variable '.' association_path_expression
//      | '(' select_statement ')'
      ) ( AS? entityAlias )?
    ;

where_clause    : ( WHERE condition_expression )? ;
group_by_clause : GROUP BY group_by_item ( ',' group_by_item )* ;
group_by_item
    : expression
//    | rollup_spec
//    | cube_spec
//    | grouping_sets_spec
//    | grand_total
    ;
having_clause   : HAVING condition_expression ;

order_by_clause
    : ORDER BY order_by_expression ( ',' order_by_expression )*
    ( LIMIT expression )?
    ( OFFSET expression | JUMP TO PAGE CONTAINING parameterExpression )?
    ;
order_by_clause_subquery
    : ORDER BY order_by_expression ( ',' order_by_expression )*
    (
       LIMIT expression
     | ( LIMIT expression )? OFFSET expression
    )
    ;
order_by_expression
    : expression ( ASC | DESC )? ( NULLS ( FIRST | LAST ) )?

window_clause
    : WINDOW windowName AS '(' window_definition ')' ( ',' windowName AS '(' window_definition ')' )*
    ;

window_definition
    : existing_window_name?
      ( PARTITION BY expression ( ',' expression )* )?
      ( ORDER BY order_by_expression ( ',' order_by_expression )* )?
      ( ( RANGE | ROWS ) ( frame_start | BETWEEN frame_spec AND frame_spec ) )?
    ;

frame_spec
    : UNBOUNDED PRECEDING
    | literalValue PRECEDING
    | CURRENT ROW
    | literalValue FOLLOWING
    | UNBOUNDED FOLLOWING
    ;

----

=== Insert statement

[source,antlr4]
----
dml_attribute
    : attributePath
    | 'KEY' '(' attributeName ')'
    | 'INDEX' '(' attributeName ')'
    ;
insert_statement : INSERT INTO entityName ( '.' collectionName )? '(' dml_attribute (',' dml_attribute)* ')' select_statement returning_clause? ;
----

=== Update statement

[source,antlr4]
----
update_statement : UPDATE entityName ( '(' collectionName ')' )? ( AS? entityAlias )? SET dml_attribute '=' expression ( ',' dml_attribute '=' expression )* where_clause? returning_clause?
----

=== Delete statement

[source,antlr4]
----
delete_statement : DELETE FROM? entityName ( '(' collectionName ')' )? ( AS? entityAlias )? where_clause? returning_clause?
----